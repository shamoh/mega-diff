allprojects {
    plugins.withType(JavaPlugin) {

        //Configures the classpath and the source and resource directories of the integrationTest task.
        sourceSets {
            //Declaring this source set creates compile time and runtime configurations for it.
            integrationTest {
                java {
                    compileClasspath += configurations.provided + main.output + test.output
                    runtimeClasspath += configurations.provided + main.output + test.output
                    srcDir file('src/integration-test/java')
                }
                resources.srcDir file('src/integration-test/resources')
                output.resourcesDir = 'build/classes/integration-test'
                output.classesDir = 'build/classes/integration-test'
            }
        }

        //Configures the compile and runtime configurations for our integration tests
        //and includes the dependencies of the test task.
        configurations {
            integrationTestCompile.extendsFrom testCompile
            integrationTestRuntime.extendsFrom testRuntime
        }

        //Create the task that runs the integration tests found from the
        //configured source directory and uses the correct classpath.
        task integrationTest(type: Test, dependsOn: jar) {
            group 'Verification'
            description 'Runs the integration tests.'
            testClassesDir = sourceSets.integrationTest.output.classesDir
            classpath = sourceSets.integrationTest.runtimeClasspath
            systemProperties['jar.path'] = jar.archivePath

            //If you want to ensure that integration tests are run every time when you invoke
            //this task, uncomment the following line.
            //outputs.upToDateWhen { false }
        }

        //Ensure that the check task fails the build if there are failing integration tests.
        check.dependsOn integrationTest
        //Ensure that our unit tests are run before our integration tests
        integrationTest.mustRunAfter test

        //Ensure that the HTML reports of unit and integration tests are written to different directories.
        tasks.withType(Test) {
            reports.html.destination = file("${reporting.baseDir}/${name}")
        }
    }
}
